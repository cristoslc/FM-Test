/**
 * =====================================
 * TestLog
 *
 * PURPOSE:
 *		Accumulates the test log entries already generated by the other Test* functions
 *		into a single list. The log is formatted according to the Test Anything Protocol
 *		(TAP).
 *
 * RETURNS:
 *		A return-delimited list of TAP log entries generated by the previous Test*
 *		function calls in the same local context.
 *
 * PARAMETERS: none
 *
 * DEPENDENCIES: none
 *
 * VARIABLES:
 *		$~test.count
 *		$~test.log[n]
 *		$~test.meta
 *		$~test.plan
 *
 * HISTORY:
 *		CREATED on 2012-06-29 by Jeremy Bante <http://scr.im/jbante>.
 *
 * REFERENCES:
 *		http://testanything.org/
 * =====================================
 */

Let ( [
	~version = "TAP version 13";
	$~test.i = $~test.i + 1
];
	// only include TAP version, meta information, and test plan in first function call
	If ( $~test.i = 1;
		~version & ¶
		& If ( not IsEmpty ( $~test.meta );
			"	---"
			& Substitute ( ¶ & $~test.meta ; ¶ ; "¶	" )	// indent YAML
			& "¶	...¶"
		)
		& If ( not IsEmpty ( $~test.plan ) ; $~test.plan & ¶ )
	)

	& $~test.log[$~test.i]

	& If ( $~test.i < $~test.count;	// there are more tests to log
		¶ & TestLog;
	/* Else */
		Let ( [
			$~test.i = ""	// clear variable for future use
		];
			If ( IsEmpty ( $~test.plan );	// there wasn't an opening plan
				¶ & "1.." & $~test.count
			)
		)
	)
)